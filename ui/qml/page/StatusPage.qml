pragma ComponentBehavior: Bound
pragma ValueTypeBehavior: Assertable
import QtQuick
import QtQuick.Layouts
import QtQuick.Templates as T
import Qcm.Material as MD
import waywallen.ui as W

MD.Page {
    id: root
    padding: 0
    showHeader: MD.MProp.size.isCompact
    showBackground: false
    title: qsTr("Status")

    actions: [
        MD.Action {
            icon.name: MD.Token.icon.extension
            text: qsTr("Plugins")
            onTriggered: MD.Util.showPopup('waywallen.ui/PagePopup', {
                source: 'waywallen.ui/PluginManagePage'
            }, root.Window.window)
        },
        MD.Action {
            icon.name: MD.Token.icon.settings
            text: qsTr("Settings")
            onTriggered: MD.Util.showPopup('waywallen.ui/PagePopup', {
                source: 'waywallen.ui/SettingsPage'
            }, root.Window.window)
        },
        MD.Action {
            icon.name: MD.Token.icon.info
            text: qsTr("About")
            onTriggered: MD.Util.showPopup('waywallen.ui/PagePopup', {
                source: 'waywallen.ui/AboutPage'
            }, root.Window.window)
        }
    ]

    component SectionTitle: MD.Text {
        typescale: MD.Token.typescale.title_medium
        color: MD.Token.color.on_surface
    }

    component SectionHint: MD.Text {
        Layout.fillWidth: true
        typescale: MD.Token.typescale.body_medium
        color: MD.Token.color.on_surface_variant
        wrapMode: Text.WordWrap
    }

    component SectionPane: MD.Pane {
        Layout.fillWidth: true
        radius: 16
        padding: 16
        backgroundColor: MD.MProp.color.surface
    }

    W.HealthQuery {
        id: healthQuery
    }

    W.RendererListQuery {
        id: rendererQuery
    }

    W.RendererPluginListQuery {
        id: pluginQuery
    }

    W.SettingsGetQuery {
        id: settingsQuery
    }

    // Queries fan out only after the daemon is Ready (avoid hitting
    // a half-booted daemon at UI startup). `daemonReady` is edge-
    // triggered, so pages constructed AFTER ready also need the level
    // check in `Component.onCompleted`.
    Connections {
        target: W.Notify
        function onDaemonReady() {
            root.reloadAll();
        }
        function onSettingsChanged() {
            settingsQuery.reload();
        }
        function onPluginChanged() {
            pluginQuery.reload();
        }
    }

    Component.onCompleted: {
        if (W.Notify.daemonPhase === W.Notify.DaemonPhase.Ready)
            reloadAll();
    }

    function reloadAll() {
        healthQuery.reload();
        rendererQuery.reload();
        pluginQuery.reload();
        settingsQuery.reload();
    }

    function rendererLabel(d) {
        const name = (d && d.name && d.name.length) ? d.name : qsTr("Renderer");
        const pid = (d && d.pid) ? d.pid : 0;
        return name + "-" + pid;
    }

    function rendererStatusLabel(status) {
        switch (status) {
        case "playing": return qsTr("Playing");
        case "paused": return qsTr("Paused");
        case "muted": return qsTr("Muted");
        default: return status;
        }
    }

    function healthStateLabel(state) {
        if (state === "healthy")
            return qsTr("Healthy");
        return state || qsTr("Unknown");
    }

    function typeLabel(type) {
        switch (type) {
        case "image": return qsTr("Image");
        case "video": return qsTr("Video");
        case "scene": return qsTr("Scene");
        default: return type;
        }
    }

    function desktopLabel(value) {
        const raw = (value || "").trim();
        if (!raw.length)
            return "";
        const key = raw.toLowerCase();
        if (key === "cosmic")
            return "COSMIC";
        if (key === "gnome")
            return "GNOME";
        if (key === "kde")
            return "KDE";
        if (key === "hyprland")
            return "Hyprland";
        if (key === "niri")
            return "Niri";
        if (key === "river")
            return "River";
        if (key === "sway")
            return "Sway";
        return raw.charAt(0).toUpperCase() + raw.slice(1);
    }

    W.RendererKillQuery {
        id: killQuery
        onStatusChanged: {
            if (status === 3) {
                rendererQuery.reload();
                healthQuery.reload();
            }
        }
    }

    MD.Dialog {
        id: killDialog
        property string rendererId: ""
        property string label: ""
        title: qsTr("Kill renderer?")
        parent: T.Overlay.overlay
        standardButtons: T.Dialog.Cancel | T.Dialog.Ok

        contentItem: MD.Text {
            text: qsTr("Stop the renderer process \"%1\"?\nUnsaved frame state may be lost.").arg(killDialog.label)
            typescale: MD.Token.typescale.body_medium
            color: MD.Token.color.on_surface_variant
            wrapMode: Text.WordWrap
        }

        onAccepted: {
            killQuery.rendererId = killDialog.rendererId;
            killQuery.reload();
        }
    }

    contentItem: MD.VerticalFlickable {
        id: m_flick
        topMargin: 12
        leftMargin: 12
        rightMargin: 12
        bottomMargin: 12

        ColumnLayout {
            width: m_flick.contentWidth
            spacing: 12

            // --- Daemon ---
            SectionPane {
                contentItem: ColumnLayout {
                    spacing: 8

                    SectionTitle {
                        text: qsTr("Daemon")
                    }

                    RowLayout {
                        spacing: 8
                        MD.Text {
                            text: qsTr("Service:")
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }
                        MD.Text {
                            text: healthQuery.service || "—"
                            typescale: MD.Token.typescale.body_medium
                            color: MD.Token.color.on_surface
                        }
                        W.Tag {
                            readonly property string label: root.desktopLabel(W.Notify.displayBackend.desktop)
                            Layout.alignment: Qt.AlignVCenter
                            visible: label.length > 0
                            text: label
                            bgColor: MD.Token.color.secondary_container
                            fgColor: MD.Token.color.on_secondary_container
                        }
                        W.Tag {
                            Layout.alignment: Qt.AlignVCenter
                            visible: W.Notify.displayBackend.flatpakId.length > 0
                            text: qsTr("Flatpak")
                            bgColor: MD.Token.color.tertiary_container
                            fgColor: MD.Token.color.on_tertiary_container
                        }
                    }

                    RowLayout {
                        spacing: 8
                        MD.Text {
                            text: qsTr("State:")
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }

                        Rectangle {
                            Layout.preferredWidth: 8
                            Layout.preferredHeight: 8
                            radius: 4
                            color: healthQuery.state === "healthy" ? MD.Token.color.primary : MD.Token.color.error
                        }

                        MD.Text {
                            text: root.healthStateLabel(healthQuery.state)
                            typescale: MD.Token.typescale.body_medium
                            color: MD.Token.color.on_surface
                        }
                    }
                }
            }

            // --- Active Renderers ---
            SectionPane {
                contentItem: ColumnLayout {
                    spacing: 8

                    SectionTitle {
                        text: qsTr("Active Renderers")
                    }

                    SectionHint {
                        readonly property var liveRenderers: W.App.rendererManager.renderers
                        visible: !liveRenderers || liveRenderers.length === 0
                        text: qsTr("No active renderers")
                    }

                    ListView {
                        Layout.fillWidth: true
                        Layout.preferredHeight: contentHeight
                        implicitHeight: contentHeight
                        interactive: false
                        spacing: 4

                        // Live, push-updated. Backend events (RendererSnapshot /
                        // RendererChanged / RendererRemoved) flow through
                        // RendererManager so a child process exiting drops out of
                        // this list without needing a manual refresh.
                        model: W.App.rendererManager.renderers

                        delegate: MD.ListItem {
                            required property var modelData

                            width: ListView.view.width
                            radius: 12
                            text: root.rendererLabel(modelData)
                            font.family: "monospace"
                            supportText: root.rendererStatusLabel(modelData.status || "")
                                + " · " + qsTr("%1 fps").arg(modelData.fps || 0)
                                + (modelData.textureWidth ? " · " + modelData.textureWidth + "×" + modelData.textureHeight : "")
                            leader: MD.Icon {
                                name: modelData.status === "paused" ? MD.Token.icon.pause : MD.Token.icon.play_arrow
                                size: 24
                                color: modelData.status === "paused" ? MD.Token.color.on_surface_variant : MD.Token.color.primary
                            }
                            trailing: RowLayout {
                                spacing: 6
                                W.GpuTag {
                                    Layout.alignment: Qt.AlignVCenter
                                    drmRenderMajor: modelData.drmRenderMajor || 0
                                    drmRenderMinor: modelData.drmRenderMinor || 0
                                }
                                MD.IconButton {
                                    icon.name: MD.Token.icon.close
                                    onClicked: {
                                        killDialog.rendererId = modelData.id;
                                        killDialog.label = root.rendererLabel(modelData);
                                        killDialog.open();
                                    }
                                }
                            }
                        }
                    }
                }
            }

            // --- Components ---
            SectionPane {
                contentItem: ColumnLayout {
                    spacing: 8

                    SectionTitle {
                        text: qsTr("Components")
                    }

                    SectionHint {
                        typescale: MD.Token.typescale.label_medium
                        visible: pluginQuery.supportedTypes && pluginQuery.supportedTypes.length > 0
                        text: qsTr("Supported types: %1").arg(pluginQuery.supportedTypes
                            ? pluginQuery.supportedTypes.map(root.typeLabel).join(", ") : "")
                    }

                    SectionHint {
                        visible: !pluginQuery.renderers || pluginQuery.renderers.length === 0
                        text: qsTr("No components")
                    }

                    ListView {
                        Layout.fillWidth: true
                        Layout.preferredHeight: contentHeight
                        implicitHeight: contentHeight
                        interactive: false
                        spacing: 4

                        model: pluginQuery.renderers

                        delegate: MD.ListItem {
                            id: componentItem
                            required property var modelData

                            readonly property bool hasSettings: (modelData.settings && modelData.settings.length > 0) === true

                            width: ListView.view.width
                            radius: 12
                            text: modelData.name || ""
                            supportText: (modelData.types ? modelData.types.map(root.typeLabel).join(", ") : "")
                            leader: MD.Icon {
                                name: MD.Token.icon.extension
                                size: 24
                                color: MD.Token.color.on_surface_variant
                            }
                            trailing: RowLayout {
                                spacing: 4
                                W.Tag {
                                    Layout.alignment: Qt.AlignVCenter
                                    text: qsTr("v") + (componentItem.modelData.version || "0.0.0")
                                }
                                MD.IconButton {
                                    visible: componentItem.hasSettings
                                    icon.name: MD.Token.icon.settings
                                    onClicked: {
                                        const name = componentItem.modelData.name;
                                        const p = settingsQuery.plugins ? settingsQuery.plugins[name] : undefined;
                                        MD.Util.showPopup('waywallen.ui/PagePopup', {
                                            source: 'waywallen.ui/PluginSettingsPage',
                                            props: {
                                                pluginName: name,
                                                schemaList: componentItem.modelData.settings || [],
                                                allCurrentPlugins: settingsQuery.plugins || ({}),
                                                currentGlobal: settingsQuery.global || ({}),
                                                currentValues: p || ({})
                                            }
                                        }, root.Window.window);
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
