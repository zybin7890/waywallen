pragma ComponentBehavior: Bound
import QtQml
import QtQuick
import waywallen.control as WC
import Qcm.Material as MD

QtObject {
    id: root
    property var filter: null
    property string value: ""
    property int condition: WC.StringCondition.STRING_CONDITION_UNSPECIFIED
    property WC.wallpaperStringFilter subfilter
    property var supportedTypes: []
    property bool _syncing: false

    readonly property var conditionModel: [
        { name: qsTr("is"),     value: WC.StringCondition.STRING_CONDITION_IS },
        { name: qsTr("is not"), value: WC.StringCondition.STRING_CONDITION_IS_NOT },
        { name: qsTr("any"),    value: WC.StringCondition.STRING_CONDITION_UNSPECIFIED }
    ]

    readonly property var typeOptions: {
        const src = supportedTypes && supportedTypes.length > 0
                  ? supportedTypes
                  : ["image", "video", "scene"];
        return src.map(t => ({ name: root.typeLabel(t), value: t }));
    }

    function typeLabel(type) {
        switch (type) {
        case "image": return qsTr("Image");
        case "video": return qsTr("Video");
        case "scene": return qsTr("Scene");
        default: return type;
        }
    }

    function labelFor(v) {
        const item = typeOptions.find(e => e.value === v);
        return item ? item.name : v;
    }

    readonly property Component valueDelegate: Component {
        MD.InputChip {
            id: valueChip
            visible: root.condition !== WC.StringCondition.STRING_CONDITION_UNSPECIFIED
            text: root.labelFor(root.value)
            onClicked: valueMenu.open()

            MD.Menu {
                id: valueMenu
                parent: valueChip
                y: parent.height
                model: root.typeOptions
                contentDelegate: MD.MenuItem {
                    required property var modelData
                    text: modelData.name
                    onClicked: {
                        root.value = modelData.value;
                        valueMenu.close();
                    }
                }
            }
        }
    }

    function syncFromFilter() {
        if (!filter)
            return;
        if (!filter.hasStringFilter)
            filter.stringFilter = subfilter;
        const active = filter.hasStringFilter ? filter.stringFilter : subfilter;
        _syncing = true;
        condition = active.condition;
        value = active.value;
        _syncing = false;
    }

    function commitToFilter() {
        if (!filter || _syncing)
            return;
        subfilter.condition = condition;
        subfilter.value = value;
        filter.stringFilter = subfilter;
    }

    onFilterChanged: syncFromFilter()
    onConditionChanged: commitToFilter()
    onValueChanged: commitToFilter()
}
